
!! !!!The Somali morphophonological/twolc rules file 


 ! ------------------------- !
 !  Morphophonological notes !
 ! ------------------------- !
	
	! Phonological Processes in Somali
	!
	!	Somali has several phonological alternations involving reduplication, lenition, vowel harmony
	!	and tone.
	!
	!	The hopes with this documentation is that it will either make twolc rules clearer, or help
	!	if it comes time to completely redo all the rules.

	! Spreading processes

	! Lenition
	!
	!	The lenis stop series in Somali <b d g> alternates with the fortis series <t k>, note that
	!	the <b> does not actually participate. Lenis stops are found in coda positions, and fortis
	!	stops are found elsewhere. The alternation occurs in both nouns and verbs.
	!
	!		ilig 'tooth'   ~   iligga 'tooth (Def.)'     ~   ilko 'teeth (Indef.)'
	!		arag 'see'     ~   aragtaa '2Sg/3SgF sees'   ~   arkaa '1Sg/3SgM sees'

	! Voicing assimilation
	!   
	!	Stops assimilate for voicing (or lenis/fortis), particularly across morpheme boundaries,
	!	however they only assimilate if they share place of articulation.
	!
	!		aragtaa 		'2Sg/3SgF sees'
	!
	!		wararka			'the news'
	!		buugga 			'the book'
	!
	!		naagta			'the woman'
	!		jaamacadda 		'the university'
	!
	!	This also follows for the retroflex segment <dh>, however the sequence <dhdh> is shortened
	!	to <dh>.
	!
	!		gabadh			'(a) girl'
	!		gabadha			'the girl'

	! Vowel ablaut
	!
	!	Vowels are subject to two main types of ablaut: (1) full ablaut across back consonants,
	!	and (2) partial ablaut with <a> ~ <e> preceding the high vowel <i>.
	!
	!	Full ablaut is constrained to morpheme boundaries: and most commonly occurs in the 'waxa'
	!	focus marker.
	!
	!		waxaan 'foc+1Sg'			magac						rah
	!		wuxuu  'foc+3SgM'			magucu	/ magacu			ruhu
	!									magicii	/ magicii 

	!	Full ablaut appears to be optional in some words.
	!
	!	Partial ablaut occurs in verbal infinitives with mostly any word of the pattern CaC. When
	!	the infinitive ending <i> is appended, <a> raises to <e>. It also occurs around person
	!	suffixes and tense ending in <e>
	!
	!		tag 'go'		tegi 'to go'			tegeen 'they went'
	!		bax 'leave'		bexi 'to leave'			bexeen 'they left'

	! <k> deletion
	!	
	!	<k> is deleted when it follows a back consonant (which is not <k> itself):
	!
	!		wax + ka => waxa
	!		magac + ka => magaca
	!		rah + ka => raha
	!

	! <l> + <t> -> <sh>
	!
	!	Occurs only across morpheme boundaries, in verbs and nouns.
	!
	!		wiilal		=>		wiilasha
	!		'boys'		  		'the boys'

	!		maqal		=>		maqashay
	!		'listen'			'2Sg/3SgF listened'


	! Vowel harmony
	!
	!	For a complete description of vowel harmony, see the literature.
	!
	!	Short summary: +ATR is the active spreading feature, and spreads leftward across all
	!	boundaries but is blocked by pauses in speech (Intonation Phrase). +ATR can spread
	!	rightward, but stops at the word boundary. It does not cross clitics, typically.
	!
	!	Hopefully VH will be covered in this analyzer.

	! Tonal Processes
	!
	!	In certain environments Somali appears to dislike having two tones immediately next
	!	to eachother. I have some collected environments for a phonology paper. May not be
	!	super important right off the bat.

	! Reduplication
	!	
	!	Somali has two kinds of reduplication: partial and full. Reduplication is typically
	!	a strategy for marking plural in nouns and adjectives in some declensions, but also
	!	appears in verbs as a derivational process. The inflectional processes are quite
	!	productive, but the derivational processes are not as productive.
	!
	!	Partial reduplication occurs in the 4th declension of nouns, but a subtype of these
	!	4th declensional nouns also has full reduplication. Partial reduplication includes
	!	epenthesis of <a>. Note that partial reduplication in adjectives is prefixing,
	!	while in nouns it is suffixing. Also, the template is slightly different.
	!
	!		af 'mouth, language'		=>		afaf	'... +Pl'
	!		qoys 'family'				=>		qoysas	'... +Pl'
	!
	!	*Note: There may be some partial reduplication that also copies the vowel.
	!
	!	Full reduplication also applies to some of these nouns:
	!
	!		qof 'person'				=> 		qofqof
	!		cad 'part'					=>		cadcad
	!
	!	Adjectives:
	!
	!		quruxsán 'beautiful'		=>		qurquruxsán ' ... +Pl'
	!		fudúd 'easy, light'			=>		fudfudúd
	!		adág 'difficult'			=>		ad'adág
	!		yár 'a little'				=>		yaryára
	!
	!		gaabán 'short'				=>		gaaggaabán ' ... +Pl'
	!		laabán 'folded'				=>		laallaabán
	!		jabán 'broken'				=>		'jajabán'
	!
	!	Irregular adjectives:
	!
	!		dhéer 'long, tall (sg)'		=>		dhaadhéer
	!		wéyn 'big'					=>		waawéyn
	!
	!	Note: Other possibilities might be found greping: with '^\(.\)aa\1e', '^\(.\)aa\1\1aa',
	!		 '\(.\)a\1$'

	! Person morphemes
	!	
	!	The realization of personal suffixes on verbs is a little complex and depends mostly on
	!	declension type and whether or not the suffix is preceded by the progressive.
	!	
	!	Realization of these suffixes is currently all handled by twolc:
	!		
	!						sg			pl				
	!			1			{đ}			{ñ} 	==>		
	!			2			{ŧ}			0				
	!			3 Masc		{đ}			{đ}				
	!			3 Fem	    {ŧ}							
	!
	!			Declension 1
	!	
	!						sg			pl					
	!			1			keenaa		keennaa			1		dhacay		dhacnay
	!			2			keentaa		keenaan			2		dhacday		dheceen
	!			3 Masc		keenaa		keentaan		3 Masc	dhacay		dhecdeen
	!			3 Fem		keentaa						3 Fem	dhacday
	
	!			Declension 2
	!
	!						sg			pl						sg
	!			1			sameeyay	sameynay		1		kariyay		karinay
	!			2			sameysay	sameyseen		2		karisay		kariseen
	!			3 Masc		sameeyay	sameeyeen		3 Masc	kariyay		kariyeen
	!			3 Fem		sameysay					3 Fem	karisay

	!			Declension 3

	!						sg			pl					
	!			1			furay		furannay			1		watay		wadannay
	!			2			furtay		furateen			2		wadatay		wadateen
	!			3 Masc		furay		fureen				3 Masc	watay		wateen
	!			3 Fem		furtay							3 Fem	wadatay

	
	! 	A short summary:
	
	!			%{đ%} - 1Sg, 3SgM and 3Pl; 	realized as 0/y/d in classes
	!			%{ŧ%} - 2Sg, 3SgF, 2Pl; 	realized as t/s in classes 
	!			%{ñ%} - 1Pl;				realized as n in all classes
	!										  'special' here just in case.

	!	This process could easily be removed from twolc and done in just flag diacritics, why not?

	! More processes to describe:
	!
	! wataa / wadatay dissimilation
	! a/e alternation in -e nouns (noun type is a bit more complex too, as corpus reveals)
	! vowel deletion ilig, ilko
	! e/y alternation in samee- verbs
	! w/b alternations
	! vowel lengthening magaalo / magaalooyin



! Notes:
!  If forms disappear but the rule should work, make sure
!  that the pair transforming exists in the Alphabet

Alphabet

 a b c d e f g h i j k l m n o p q r s t u v w x y z ' ´ ´´ 
 
!! For ease of reduplication and treating sh as separate from l + t => sh

 sh kh dh 
  
 %^V:0
 %^:0
 %{N%}:0
 
 ! Morphophon
 %{%-I%}:0
 %{%-V%}:0
 %{%-a%}:0
 %{%-e%}:0
 %{C2%}
 %{E%}:e
 %{a%}            ! V3B with -a root deletion
 %{a%}:0
 %{ae%}
 %{d%}:d
 %{e%}
 %{m%}
 %{mm%}:m 
 %{n%}:n
 %{o%}
 %{y%}:y

 ! Boundary markers
 ! Ideally %> for root boundary, # for word-final. Pro'lly inconsistent
 ! about this, but something worth going through and checking.
 ! For testing remove :0 to see boundaries
 %>:0
 #:0 
:

! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <
  
  %-
  ':0 !'

! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefx
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point
 

 %{t%}:t

 ! Trigger for t + d contraction in 3B
 ! Also ablaut blocker
 %{X%}:0
 %{e%} 

;

Sets

 Vowels = a e i o u y %{ae%} %{o%} %{e%} %{a%} ;
 
 BackV =  a e i o u ; ! aa ee ii oo uu ;
 FrontV = ä ë ï ö ü ; ! ää ëë ïï öö üü ; 

 Cons = b c d f g h j k l m n p q r s t v w x y z dh %{m%} %{mm%} %{n%} kh sh ' ;
 Back = c x h ' q kh ;

 TDalt = q kh c x h ' d w y ;
 

 VceStop = b d g dh ;
 VcelStop = t k ;

 Letters = a b c d e f g h i j k l m n o p q r s t u v w x y z ' ´
			dh sh kh 
			;

 MorphoPhon =
			# %{m%} %{C2%} %{C3A%} %{C3B%} %{C3C%} %{t%} %{d%} %{X%} 
			%{ae%} %{e%} %{n%} %{o%} %{ATR%} 
			;
 Tones = ´ ´´ ;

 WordChars = Letters MorphoPhon ;

Definitions
 AllBoundaries = [ #: | %>: | %> | %>: ] ;
 WordBoundary  = #: ;

 TonePattern = [ ´ | ´´ ] ;
 PostN = TonePattern %>: %{N%}:0 ;

Rules

! TODO: write tests for gacmahii / gacmihii, magacii / magicii, magucu / magacu

! TODO: tests from these

!! ilig+N+Masc+Pl+Def+PxSg3M+Abs+Prox       il^ik´%>%{N%}a%>híisa#
!!   -- > ilkihiisa 
!! walaal+N+Masc+Pl+Def+PxSg3M+Abs+Prox     walaal´´%>%{N%}a%>híisa#
!!   -- > walaalihiisa 

!! magac+N+Masc+Sg+Def+Abs+Dist				magac´´%>%{N%}kii#
!!   -- > magicii 
!! magac+N+Masc+Sg+Def+Nom+Prox				magac´´%>%{N%}ku#
!!   -- > magucu 
!! magac+N+Masc+Sg+Def+PxSg3F+Abs+Prox      magac´´%>%{N%}kíisa#
!!   -- > magiciisa 
!! waxa+CS+Foc/R+Sg3M      wax%>uu#
!!   -- > wuxuu

!! wax+N+Masc+Sg+Def+PxSg3M+Nom+Prox		wax´´%>%{N%}kíisu#
!!   -- > wixiisu / waxiisu


! getting waxuu; tone marker blocks.
"Ablaut around back fricatives at morpheme boundaries"
!! __@RULENAME@__  
 a:Cy <=> Cons _ Back ( Tones ) PostN ( k:0 ) Cy ;					!  ?
		  Cons _ Back ( Tones ) PostN k:0 Cy ;						! magucu
		  Cons _ Back ( Tones ) %>: :Cy ;				    ! wuxuu

	where Cy in (u i)

	matched ;

! aabb{-e}{e}´´>{N}h
! aabb{e}´´>{N}hii

"Ablaut in declension 7 with final vowel"
!! __@RULENAME@__  
 %{e%}:Cy <=> Cons (%{%-e%}:0) _ PostN h Cy ;

	where Cy in (u i)

	matched ;


! #LT_SH

"LT -> SH part 1"
!! __@RULENAME@__  
 l:s <=> _ ( Tones ) %>: ( %{N%}:0 ) ( %>: ) t: ;
 ! Also for V3B where there is d in triggering environment and interceding {a}
         _ %{a%}:0 ( Tones ) %>: ( %{N%}:0 ) ( %>: ) d: ;
 ! Also for V1 where a vowel deletes
         _ %{%-V%}:0 ( Tones ) %>: ( %>: ) t: ;


"LT -> SH part 2"
!! __@RULENAME@__  
 t:h <=> l: ( Tones ) %>: ( %{N%}:0 ) ( %>: ) _ ;
         l: %{%-V%}:0 ( Tones ) %>: ( %{N%}:0 ) ( %>: ) _ ;

         ! In reduplicative plurals where l is reduplicated
         l: PostN [ Vowels | Vowels+ ] %{C2%}:s ( [ %>: | %>:+ ] ) _ ;
         l: Tones %{N%}:0 a %{C2%}:s %>: _ ;


"LT -> SH part 2 for V3B"
!! __@RULENAME@__  
 d:h <=> l: %{a%}:0 ( Tones ) %>: ( %>: ) _ ;

"LT -> SH in reduplicative plurals, pt 1"
!! __@RULENAME@__  
 %{C2%}:s <=> l Tones %>: %{N%}:0 a _ %>:  t: ;

"V2 {y} deletion"
!! __@RULENAME@__  
 %{y%}:0 <=> _ ( Tones ) %>:+ y ;
 			 y _ ;
 			 i i _ ( Tones ) %>:+ Cons;
 			 i _ ( Tones ) %>:+ Cons;
 			 _ %>: WordBoundary ;

"Definite suffix -k to -g next to preceding voiced"
!! __@RULENAME@__  
 k:g <=> [ :VC | Vowels ] PostN _ Vowels ;
 		 %{C2%}:g %>: _ Vowels ;
	where VC in (g y w) ;

"t:0 before d h"
!! __@RULENAME@__  
 CD:0 <=> d h Tones %>: %{N%}:0 _ Vowels ;
    where CD in (d t) ;

"t/d alternation - definite suffix, V1"
!! __@RULENAME@__  
 t:d <=> 
         ! In verbs
         TDalt (%{%-V%}:0) ( Tones ) %>: _ ;
         i     (%{%-V%}:0) ( Tones ) %>: _ ;
         ! Definite suffix: dhexda, xuquuqda
         TDalt  PostN _ Vowels ;
         Vowels PostN _ Vowels ;

! TODO: dukaammo
! baabuurro, dukaammo, albaabbo
! Except words like guri/bangi, which have y.
"Declension 2: -C doubling in plural"
!! __@RULENAME@__  
 %{C2%}:Cz <=> :Cz PostN _ ( %>: ) PlVowels ;
	where Cz in (b d dh l r n m)
	PlVowels in (a o) ;

! TODO: This rule is currently conflicting with the next one.
! bangiyo
! TODO: guriyo, guryo
"Declension 2: -y insertion after back sounds and fricatives; and after -i"
!! __@RULENAME@__  
 %{C2%}:y <=> Cz PostN _ ( %>: ) PlVowels ; 
			  i PostN _ ( %>: ) PlVowels ;
	where Cz in (w c q x s j)
	PlVowels in (a o) ;


! miisas, qoysas, afaf
"Declension 4: Plural Partial Reduplication"
!! __@RULENAME@__  
 %{C2%}:Cz <=> Cz PostN a _ ; 
	where Cz in Cons
	matched ;

"Declension 4: Plural Partial Reduplication %{m}"
!! __@RULENAME@__  
 %{C2%}:m <=> %{m%}: PostN _ %>: a ;

"Declension 4: C2->n in m contexts"
!! __@RULENAME@__  
 %{C2%}:n <=> :m Tones %>: %{N%}:0 a _ %>: [ k t n ] ;
 			  :m ( Tones ) %>: %{N%}:0 ( Vowels ) _ ;

! yar´>{N}{C3A}{C3B}{C3C}>#

!! "Declension 4: Plural Full Reduplication - pt. 1"
!!  %{C3A%}:Cz <=> Cz WordChars WordChars ( Tones ) %>: %{N%}:0 _ ;
!!     matched ;

!! "Declension 4: Plural Full Reduplication - pt. 2"
!!  %{C3B%}:Cz <=> WordChars Cz WordChars ( Tones ) %>: %{N%}:0 _ ;
!!     where Cz in Cons
!!     matched ;
!! 
!! "Declension 4: Plural Full Reduplication - pt. 3"
!!  %{C3C%}:Cz <=> WordChars WordChars Cz ( Tones ) %>: %{N%}:0 _ ;
!!     where Cz in Cons
!!     matched ;

! guriyo
"-y- insertion in plurals"
!! __@RULENAME@__  
 0:y <=> i PostN _ VowB ;
	where VowB in (a o u e) ;

! disallow yy
"Disallow yy"
!! __@RULENAME@__  
 y:0 <=> _ [PostN | %>:] y ;


! Doesn't work with wuxu, where k has been deleted.
! Doesn't work with magicii, magucu, but should optionally. magacii and magicii, etc are both possible

! Opacity?  wax+ku -> wax+u -> wux+u

"Declension 6: final vowel"
!! __@RULENAME@__  
  a:o <=> _ PostN WordBoundary ;
  		  _ PostN o ;


! 
"Declension V1: magacow / magacaabay / magacawday, shorten a"
!! __@RULENAME@__  
 a:0 <=> a _ %>: w d ;

"Delete preceding a"
!! __@RULENAME@__  
 a:0 <=>  _ %{%-a%}:0 ;

! TRY: { WordChars - O }

! _ Tones %>: NOUN [WordChars - O] ; 

!!! 
!!! ! AllBoundaries \?:WordChars
! TODO: could also be i/a alternations in this declension

"Declension 7: {-e} delete"
!! __@RULENAME@__  
 e:0 <=> _ %{%-e%}:0 ;

"Declension 7: final vowel"
!! __@RULENAME@__  
 %{e%}:a <=> _ PostN WordChars ;

"Declension 7: final vowel in final position"
!! __@RULENAME@__  
 %{e%}:e <=> _ PostN WordBoundary ;



!!!
!!!
!!!  Verbal Rules
!!!
!!!

! {-I}:0 for derivations

"V2A -> V3B derivations, delete -i"
!! __@RULENAME@__  
 i:0 <=> Vowels Cons _ %{%-I%}:0 ;

"V2A -> V3B derivations, depalatization"
!! __@RULENAME@__  
 j:g <=> _ i:0 %{%-I%}:0 ;

! {-V}:0 for concatenative v deletion set.

! "V1: b mutation"
!  b:w <=> _ %>: Cons ;


! TODO: xfst  analyzes both, generates one for tag, but not for bex
! bax+V+Inf, tag+V+Inf

"V1: a/e umlaut before i/e"
!! __@RULENAME@__  
  %{ae%}:e <=> Cons _ Cons ( Tones ) [%>: | %>:+ ] i ;
               Cons _ Cons ( Tones ) [%>: | %>:+ ] e ;


"V1: a/e umlaut before a"
!! __@RULENAME@__  
  %{ae%}:a <=> Cons _ Cons ( Tones ) [%>: | %>:+ ] a ;
               Cons _ Cons ( Tones ) [%>: | %>:+ ] Cons ;


! "V1: a/e umlaut before i/e"
!   %{ae%}:a <=> Cons _ Cons ( Tones ) [%>: | %>:+ ] [ :I | :E ] ;

! TODO: maqallay
"V1, D3: Vowel deletion"
!! __@RULENAME@__  
 Vz:0 <=>
          ! V1 
          ! Cons+ for korodhay, etc
          %^:0 _ Cons+ ( Tones ) %>:+ Vowels ; 
          ! D3
          %^: _ Cons PostN Vowels ; 
          ! With no %^ marker, but {-V} instead
          Cons _ Cons ( Tones ) %{%-V%}:0 %>:+ [ Vowels + 0 ] ;

	where Vz in Vowels ;

    ! maqlay			maqalnay/maqallay
    ! maqashay			maqasheen
    ! maqlay			maqleen
    ! maqashay		


    ! arkay			aragnay
    ! aragtay			aragteen
    ! arkay			arkeen
    ! aragtay		

    ! inf:     1P       2P          INF
    ! -lan    -lmay    -lantay      -lmi
    ! -dub    -dbay    -dubtay      -dbi
    ! -ran    -rmay    -rantay      -rmi
    
    ! ran{-V}>0 -> rm
    ! ran{-V}>i -> rmi
    ! ran{-V}>t -> rant
    
    ! TODO: seems to be optional?
    ! echo "gulaalan+V+3SgM+Ind+Past" | xsomgen
    ! -> gulaalanay, gulaalmay


! TODO: is there a better place to do this? After twolc?
"V1: Simplification of dhd to dh (t)"
!! __@RULENAME@__  
 t:0 <=> d h ( Tones ) %>: _ ;


! "V4: Vowel deletion without preceding marker before vowels and epsilon"
!  Vz:0 <=> Cons _ Cons %{%-V%}:0 %>: [Vowels + 0] ;
!    where Vz in Vowels ;
 
"V1: n/m when deleted vowel results in C0n"
!! __@RULENAME@__  
 n:m <=> Cons Vowels:0 _ ( Tones ) %{%-V%}:0 %>:+ [ Vowels + 0 ] ;


 
!!!
!!!  V2A
!!!



!!!
!!!  V2B
!!!

"V2B: y-insertion"
!! __@RULENAME@__  
 0:y <=> [:Vowels - :y] ( Tones ) %>: _ %>: :Vowels ; 

"V2B: ey before consonants"
!! __@RULENAME@__  
 %{e%}:y <=> _ ( Tones ) %>: [:Cons - :Y] ; 

"V2B: ee before y"
!! __@RULENAME@__  
 %{e%}:e <=> _ ( Tones ) %>: :Y ; 

"V2B: Remove e"
!! __@RULENAME@__  
 %{e%}:0 <=> _ ( Tones ) %>: :Y :Cons ;


	 ! PAST

	 ! sameeyay		sameynay
	 ! sameysay		sameyseen / sameysid
	 ! sameeyay		sameeyeen
	 ! sameysay		

	 ! PROG

	 ! sameynayaa
	 ! etc...



!!!
!!!  V3A
!!!




!!!
!!!  V3B
!!!

!! Following rules are for the funky things that happen with 1Sg, 3SgM, 3Pl
!! Basically, disallowed d_d, so it gets simplified to t
!!   

!!	wada>d>aa#		wada>nn>aa#
!!  wada>t>aa#		wada>d>aan#
!!	wada>d>aa#		wada>t>aan#
!!  wada>t>aa#

!!	wataa						wadannaa
!!  wadataa						wadataan
!!	wataa						wataan
!!  wadataa


!!	fura>d>aa#		fura>nn>aa#
!!  fura>t>aa#		fura>d>aan#
!!	fura>d>aa#		fura>t>aan#
!!  fura>t>aa#

!!	furtaa						furannaa
!!  furataa						furataan
!!	furtaa						furtaan
!!  furataa

!! Also another type: TODO: merge

!! sigo+V+IV+1Sg+Ind+Past   sig{a}>d>ay#
!! sigo+V+IV+2Sg+Ind+Past   sig{a}>t>ay#

!! sigtay
!! sigatay

!! But: obd / cd / hd / qd / xd / yd
!! Note: bt / obd


"V3B: %{o%}:o finally"
!! __@RULENAME@__  
 a:o <=> _ ( Tones ) %>: WordBoundary ; 

"V3B: %{o%}:0 in d _ d"
!! __@RULENAME@__  
 a:0 <=> Vowels CorCons: _ ( Tones ) %>: d: ;
 	where CorCons in (d r s) ;

"V3B: d dissimilation"
!! __@RULENAME@__  
 d:0 <=> Vowels d: a:0 ( Tones ) %>: _ %>: ;

! TODO: write test
"V3B: d dissimilation w/ cor" 
 d:t <=> Vowels [CorCons: - d:] [ a:0 ] ( Tones ) %>: _ %>: ;
         Vowels NonCorCons %{a%}:0 ( Tones ) %>: _ ;
         Vowels s h [ a:0 %{a%}:0 ] ( Tones ) %>: _ ;
         ! arrumo -> arruntay Nasal
         Vowels :n %{a%}:0 ( Tones ) %>: _ ;

 	where CorCons in (d r s)
 	      NonCorCons in (b f g n q r) 
 	      ;

"V3B: d dissimilation 2" 
 d:t <=> Vowels _ a:0 ( Tones ) %>: d:0 %>: ; 

! TODO: write test: buqday / buqatay ; sirtay / siratay

"V3B: root {a} deletion before suffixes with -d"
!! __@RULENAME@__  
 %{a%}:0 <=> _ ( Tones ) %>: d: ;

"V3B: otherwise"
!! __@RULENAME@__  
 %{a%}:a <=> _ ( Tones ) %>: t: ;

 
!! !!  General Rules

"Vowel reduction VVV -> VV"
!! __@RULENAME@__  
 V1:0 <=> _ AllBoundaries (%>:) V1 V1 ;
  where V1 in Vowels
	matched ;

! TODO: write test: sakhraan / sakhraamo
"m->n in codas"
!! __@RULENAME@__  
 Cm:n <=> _ ( Tones ) %>: [ WordBoundary | k: | t: ] ; 
 		  _ PostN [ WordBoundary | k: | t: ] ;
 	! V3B 1P arrumo -> arruntay
 	      _ %{a%}:0 ( Tones ) %>: d: ;
 	      ! Before clitics
 	      _ PostN %>: [ Cons: - [ k: | g: ] ] ;
 	where Cm in (m %{m%}) ;

"m->m in codas"
!! __@RULENAME@__  
 %{m%}:m <=> _ ( Tones ) %>: ( %{N%}:0 ) [ Vowels | :m ] ; 
 		  _ PostN :m ;

"Optional m doubling"
!! __@RULENAME@__  
 0:m => _ %{m%}:m ;

! magac´´%>%{N%}ku# 
"Velar deletion after Back, but avoiding digraphs"
!! __@RULENAME@__  
 k:0 <=> [Letters - [d | s | k ]] Back PostN _ ;

! Final lenition appears to be final voicing because of the orthography.
! {b d g} represent lenis stops, and {p t k} represent fortis stops.
! Lenition occurs in syllable codas, leading to alternations 
! such as arkaa / aragtaa

! Some literature may refer to this as final voicing.

! TODO: write test: arag- / ark- 

"Final lenition."
!! __@RULENAME@__  
 Cv:Cx <=> Vowels _ ( Tones ) %>: ( %{N%}:0 ) [ WordBoundary | :Cons ] ;
  where Cx in (b d g)
        Cv in (p t k)
		matched ; 

"-ee changes to -ye after other vowels"
!! __@RULENAME@__  
 %{E%}:y <=> [Vowels - o] %>: _ e ;

"-ee changes to -e after y"
!! __@RULENAME@__  
 %{E%}:0 <=> y %>: _ e ;

"-ee clitic deletes preceding -o"
!! __@RULENAME@__  
 Cv:0 <=> _ %>: %{E%}:e ;
  where Cv in ( o ) ;

! dhammaan
"progressive nasal assimilation across morphological boundaries"
!! __@RULENAME@__  
 n:m <=> m %>: _ ;

!€ primus%>s
!€ primus00

!€grís%>s
!€grís00

!$grís%>s
!$grís0s

! vim: set ts=4 sw=4 tw=0 syntax=lexc :
